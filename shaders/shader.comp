#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WORKGROUP_SIZE 32
layout (local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1 ) in;

struct Pixel{
  vec4 value;
};

layout(std430, binding = 0) buffer outputBuffer
{
  Pixel imageData[];
};

layout(std430, binding = 1) buffer inputBuffer
{
  int data[];
};

void main() {

  /*
  In order to fit the work into workgroups, some unnecessary threads are launched.
  We terminate those threads here. 
  */
  if(gl_GlobalInvocationID.x >= data[0] || gl_GlobalInvocationID.y >= data[1])
    return;

  float x = float(gl_GlobalInvocationID.x) / float(data[0]);
  float y = float(gl_GlobalInvocationID.y) / float(data[1]);

  /*
  What follows is code for rendering the mandelbrot set. 
  */
  vec2 uv = vec2(x,y);
  float n = 0.0;
  vec2 c = vec2(-.445, 0.0) +  (uv - 0.5)*(2.0+ 1.7*0.2  ), 
  z = vec2(0.0);
  const int M = 1000;
  for (int i = 0; i<M; i++)
  {
    z = vec2(z.x*z.x - z.y*z.y, 2.*z.x*z.y) + c;
    if (dot(z, z) > 2) break;
    n++;
  }
          
  // we use a simple cosine palette to determine color:
  // http://iquilezles.org/www/articles/palettes/palettes.htm         
  float t = float(n) / float(M);
  vec3 d = vec3(0.3, 0.3 ,0.5);
  vec3 e = vec3(-0.2, -0.3 ,-0.5);
  vec3 f = vec3(2.1, 2.0, 3.0);
  vec3 g = vec3(0.0, 0.1, 0.0);
  vec3 color = vec3(d + e*cos( 6.28318*(f*t+g)));
          
  // store the rendered mandelbrot set into a storage buffer:
  imageData[data[0] * gl_GlobalInvocationID.y + gl_GlobalInvocationID.x].value = vec4(color, 1.0);
}